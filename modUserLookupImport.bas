Attribute VB_Name = "modUserLookupImport"
Option Compare Database

Sub TxtUserListImport()
    'Script used to import files from NSLookup - this is after the batch file is run
    'The script asks for the directory of the files and then generates a final Results file
    'The Results file is then loaded into a database table

    Dim strPath As String
    Dim strFile As String
    Dim strFilePath As String
    Dim header As String
    Dim entry As String
    Dim entryGrab As String
    Dim userName As String
    Dim fullName As String
    Dim comment As String
    Dim empID As String
    Dim acctActive As String
    Dim acctExpire As String
    Dim passLastSet As String
    Dim passExpire As String
    Dim passChange As String
    Dim lastLogon As String

    
    strPath = Get_Directory("Choose the folder with the files for import") & "\"    'Call the Get_Directory function for the file path
                                                                                    'A \ is also appended to the file path so it can be used later

    'Remove the resultFile from a previous run of this script
    resultFile = "C:\Scripts\UserResults.txt"
    Open resultFile For Append As #9
    Close #9
    Kill resultFile
    
    
    'Remove bogus Aud_NS file that is generated by the batch script
    bogusFile = strPath + "User_list_.txt"
    Open bogusFile For Append As #99
    Close #99
    Kill bogusFile
    
    strFile = Dir(strPath & "*.txt")    'Get the first file in the path
    
    While strFile <> ""                 'While there are still files to process the loop will continue
        header = Mid(strFile, 11, (InStr(strFile, ".") - 11))     'Get the header from the file name to know which ID is being reviewed
        strFilePath = strPath + strFile
        Open strFilePath For Input As #1    'Open the text file
        Do Until EOF(1)
            Input #1, entry
            If entry = "The command completed successfully." Then Exit Do
            entryGrab = Mid(entry, 1, 10)
            If entryGrab = "User name " Then
                userName = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Full Name " Then
                fullName = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Comment   " Then
                comment = Mid(entry, 30, Len(entry) - 29)
                If InStr(entry, "-") > 0 Then
                    empID = Mid(entry, (InStr(entry, "-") + 2), 5)
                End If
            End If
            If entryGrab = "Account ac" Then
                acctActive = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Account ex" Then
                acctExpire = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Password l" Then
                passLastSet = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Password e" Then
                passExpire = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Password c" Then
                passChange = Mid(entry, 30, Len(entry) - 29)
            End If
            If entryGrab = "Last logon" Then
                lastLogon = Mid(entry, 30, Len(entry) - 29)
                Exit Do
            End If
        Loop
        
        'Open the resultFile and poplate the header ane employee info
        Open resultFile For Append As #9
        Write #9, header, userName, fullName, comment, empID, acctActive, acctExpire, passLastSet, passExpire, passChange, lastLogon
        Close #9
        
        userName = ""
        fullName = ""
        comment = ""
        empID = ""
        acctActive = ""
        acctExpire = ""
        passLastSet = ""
        passExpire = ""
        passChange = ""
        lastLogon = ""

        Close #1        'Close the text file
        strFile = Dir() 'Pull the next file in the path
    Wend
    
    Dim strTableName As String
    Dim fileDate As String
    
    fileDate = TodayDate()
    
    'Remove previous version of this file if it was run earlier in the dat
    'It now has a date stamp included with the file name
    
    strTableName = "NSLookupResults" + fileDate
    If TableExists(strTableName) Then
        DoCmd.DeleteObject acTable, strTableName
    End If
    
    'Import the text file into a database table
    DoCmd.TransferText acImportDelim, UserResultsIS, strTableName, resultFile, False
    
    Application.RefreshDatabaseWindow
    
    MsgBox "Script Complete!!!"

End Sub
